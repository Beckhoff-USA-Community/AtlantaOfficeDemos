<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.12">
  <POU Name="FB_TiltStation" Id="{e12eac8b-45ec-4732-8a39-3b823ac4b9a5}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_TiltStation EXTENDS FB_XPlanarStationBase
VAR
	_StationAngle  : LREAL;
	ProcessState	: INT;
	ProcessStateOld	: INT;
	TON_StateChange	: TON := (PT := T#100MS);
	CommandResultDone	: BOOL;
	TiltAngle		: LREAL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="Execute" Id="{76fb1a3b-d812-4f97-98a3-cc8ff41ab473}">
      <Declaration><![CDATA[METHOD Execute
VAR_INST
	i			  : UDINT := 1;
	CommandResult : I_PlanarTaskResult;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Execute();
IF StationState = E_XPlanarStationState.ProcessMover THEN
	CASE ProcessState OF

		0:	// Raise Z
			CommandResult		:= ipCurrentMover.MoveCommand.VerticalMove(5.0);
			IF ipCurrentMover.MoverType = APM4220 THEN
				TiltAngle		:= 4.5;
			ELSIF ipCurrentMOVER.MoverType = APM4330 THEN
				TiltAngle		:= 3.0;
			ELSIF ipCurrentMover.MoverType = APM4550 THEN
				TiltAngle		:= 2.0;
			END_IF
			ProcessState		:= 1;

		1:	// Check for Result
			IF CommandResult.Done AND TON_StateChange.Q THEN
				ProcessState 	:= 2;
			END_IF

		2:	// Tilt A Positive
			CommandResult		:= ipCurrentMover.MoveCommand.TiltA(TiltAngle);
			ProcessState 		:= 3;

		3:	// Check for Result
			IF CommandResult.Done AND TON_StateChange.Q THEN
				ProcessState	:= 4;
			END_IF
			
		4:	// Tilt A Negative
			CommandResult		:= ipCurrentMover.MoveCommand.TiltA(TiltAngle * -1.0);
			ProcessState 		:= 5;

		5:	// Check for Result
			IF CommandResult.Done AND TON_StateChange.Q THEN
				ProcessState	:= 6;
			END_IF
			
		6:	// Tilt A Zero
			CommandResult		:= ipCurrentMover.MoveCommand.TiltA(0.0);
			ProcessState		:= 7;

		7:	// Check for Result
			IF CommandResult.Done AND TON_StateChange.Q THEN
				ProcessState	:= 8;
			END_IF
			
		8:	// Tilt B Positive
			CommandResult		:= ipCurrentMover.MoveCommand.TiltB(TiltAngle);
			ProcessState 		:= 9;

		9:	// Check for Result
			IF CommandResult.Done AND TON_StateChange.Q THEN
				ProcessState	:= 10;
			END_IF
			
		10:	// Tilt B Negative
			CommandResult		:= ipCurrentMover.MoveCommand.TiltB(TiltAngle * -1.0);
			ProcessState 		:= 11;

		11:	// Check for Result
			IF CommandResult.Done AND TON_StateChange.Q THEN
				ProcessState 	:= 12;
			END_IF
			
		12:	// Tilt B Zero
			CommandResult		:= ipCurrentMover.MoveCommand.TiltB(0.0);
			ProcessState 		:= 13;

		13:	// Check for Result
			IF CommandResult.Done AND TON_StateChange.Q THEN
				ProcessState 	:= 14;
			END_IF

		14:	// Lower Z
			CommandResult		:= ipCurrentMover.MoveCommand.VerticalMove(2.0);
			ProcessState 		:= 15;

		15:	// Check for Result
			IF CommandResult.Done AND TON_StateChange.Q THEN
				ProcessState 	:= 16;
			END_IF

		16:	// Done
			_StationComplete 	:= TRUE;

	END_CASE

	// Timer Call
	TON_StateChange(IN := ProcessState = ProcessStateOld);

ELSE
	ProcessState	:= 0;
	TON_StateChange(IN := FALSE);

END_IF

// Save Prevoius Values
ProcessStateOld	:= ProcessState;

// Debug
IF CommandResult <> 0 THEN
	CommandResultDone	:= CommandResult.Done;
ELSE
	CommandResultDone	:= FALSE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="StationAngle" Id="{25ec7391-fb27-4a90-a7c7-a70be54641d5}">
      <Declaration><![CDATA[PROPERTY StationAngle : LREAL]]></Declaration>
      <Set Name="Set" Id="{bd63581f-e215-4840-a342-62e568dd9daf}">
        <Declaration><![CDATA[VAR
END_VAR]]></Declaration>
        <Implementation>
          <ST><![CDATA[_StationAngle := StationAngle;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <LineIds Name="FB_TiltStation">
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_TiltStation.Execute">
      <LineId Id="1" Count="105" />
    </LineIds>
    <LineIds Name="FB_TiltStation.StationAngle.Set">
      <LineId Id="1" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>